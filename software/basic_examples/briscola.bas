10 REM *************************
20 REM *    B R I S C O L A    *
30 REM *                       *
40 REM * PORTED TO LM80C BASIC *
50 REM *  BY LEONARDO MILIANI  *
60 REM *    ORIGINAL CODE BY   *
70 REM *   C.& L. BARAZZETTA   *
80 REM *************************
90 REM
100 SCREEN4:COLOR 1,15,2:VOLUME0,15:GOSUB2000:I=RND(-ABS(TMR(0)))
110 DIMMZ(40,2),C(3,2),G(3,2),MT(10,4):FORA=1TO20
120 CX=INT(RND(1)*25):CY=INT(RND(1)*15):CV=INT(RND(1)*14+1):CS=INT(RND(1)*4)
130 GOSUB15000:GOSUB10000:NEXT:PAUSE200:CLS
140 LOCATE7,3:PRINTCHR$(217);:FORI=1TO17:PRINTCHR$(143);:NEXT:PRINTCHR$(218)
150 LOCATE7,4:PRINTCHR$(144);" B R I S C O L A ";CHR$(144)
160 LOCATE7,5:PRINTCHR$(220);:FORI=1TO17:PRINTCHR$(143);:NEXT:PRINTCHR$(219)
170 LOCATE9,8:PRINT"CONVERSIONE  DI":LOCATE8,9:PRINT"LEONARDO  MILIANI"
180 LOCATE7,14:PRINT"[I] PER ISTRUZIONI":LOCATE7,15:PRINT"[G] PER GIOCARE"
190 LOCATE7,16:PRINT"[F] PER FINIRE":A=INKEY(10)
200 A=INKEY(10):IFA=ASC("G")ORA=ASC("g")THEN500
205 IFA=ASC("I")ORA=ASC("i")THEN220
210 IFA<>ASC("F")ANDA<>ASC("f")THEN200
215 CLS:PRINT"CIAO":END
220 CLS:LOCATE6,0:PRINT"GIOCO DELLA BRISCOLA"
230 LOCATE6,1:FORI=1TO20:PRINTCHR$(184);:NEXT:LOCATE0,3
240 PRINT"Si gioca con un mazzo di 40 car-";
245 PRINT"te. La scala dei valori e' la"
250 PRINT"seguente:":PRINT
260 PRINT"CARTA          SIMBOLO    PUNTI"
270 PRINT"----           -------    -----"
280 PRINT"ASSO              A        11"
290 PRINT"TRE               3        10"
300 PRINT"RE                R        4"
310 PRINT"REGINA (DONNA)    D        3"
320 PRINT"FANTE (GOBBO)     F        2"
330 PRINT"7,6,5,4,2                  0"
340 GOSUB3000
360 CLS
370 PRINT"Il mazzo lo mischia sempre il"
380 PRINT"computer che distribuisce 3"
385 PRINT"carte ciascuno, ed alla fine"
390 PRINT"mostra l'ultima carta del mazzo"
392 PRINT"e il suo seme e' la briscola."
394 PRINT"I semi sono:":PRINT
400 PRINTCHR$(133);"Picche ";CHR$(132);"Fiori ";
410 PRINTCHR$(131);"Quadri ";CHR$(130);"Cuori":PRINT
420 PRINT"Es.: l'ultima carta e' il 5 di"
425 PRINT"cuori (5";CHR$(130);") percio' la briscola"
430 PRINT"e' cuori e tutte le carte di"
435 PRINT"cuori sono briscole."
440 GOSUB3000:CLS
442 PRINT"Si gioca buttando una carta"
445 PRINT"ciascuno. Tra le 2 carte buttate";
450 PRINT"una vince secondo queste regole:";:PRINT
460 PRINT"- se i semi delle 2 carte sono"
462 PRINT"uguali vince quella di maggior"
464 PRINT"valore;":PRINT"- se sono diversi e una carta e'";
466 PRINT"una una briscola, vince la"
468 PRINT"briscola;":PRINT"- altrimenti vince chi ha tirato";
470 PRINT"la carta per primo.":PRINT
475 PRINT"Chi ha effettuato la presa ha"
480 PRINT"diritto a pescare per primo una"
482 PRINT"carta dal mazzo e ad iniziare la";
484 PRINT"mano successiva. Si prosegue ri-";
486 PRINT"petendo le mosse precedenti fino";
488 PRINT"all'esaurimento delle carte in"
490 PRINT"mano. Alla fine della partita"
492 PRINT"vince chi ha totalizzato piu' di";
494 PRINT"60 punti."
495 GOSUB3000:CLS
500 REM *** DISPONI CARTE ***
510 CLS:PG=0:PC=0:NP=0:NF=0:NQ=0:NC=0:BC=0:NA=0:FA=0
520 LOCATE0,0:FORW=1TO32:PRINTCHR$(130+(INT(RND(1)*4)));:NEXT
530 LOCATE10,0:PRINT"  BRISCOLA  ";
540 LOCATE0,1:FORW=1TO5:PRINTCHR$(163);:NEXT:PRINT"COMPUTER";
545 FORW=1TO5:PRINTCHR$(163);:NEXT
550 LOCATE0,22:PRINT"  1     2     3"
560 LOCATE0,23:FORW=1TO4:PRINTCHR$(166);:NEXT:PRINT"GIOCATORE";
570 FORW=1TO5:PRINTCHR$(166);:NEXT
595 PAUSE50
600 PC=0:PG=0
605 LOCATE0,12:PRINT"MISCHIO IL MAZZO  ":GOSUB50000
610 LOCATE0,12
620 FORX=0TO12STEP6
622 FORY=13TO2STEP-11
625 IFY=2THENCC=1:C1=X/6+1:GOTO640
627 CC=0:G1=X/6+1
640 GOSUB45000
680 NEXTY,X
682 FORC1=1TO3
684 GOSUB55010
686 NEXTC1
690 GOSUB10000:CX=26:CY=5:CV=MZ(40,1):CS=MZ(40,2):GOSUB15000:REM BRISCOLA
700 LOCATE23,2:PRINT"BRISCOLA:";:LOCATE23,3
710 ONMZ(40,2)+1GOTO730,740,750,760
730 PRINT"PICCHE";CHR$(133);:GOTO770
740 PRINT"FIORI";CHR$(132);:GOTO770
750 PRINT"QUADRI";CHR$(131);:GOTO770
760 PRINT"CUORI";CHR$(130);
770 BR=MZ(40,2)
775 PAUSE50
780 GOSUB10000:CX=26:CY=10:CV=15:CS=1:GOSUB15000:REM MAZZO
790 GOSUB47000
795 T=2
800 FORI=1TO40STEP2
810 IFT=2THENGOSUB20000:GOSUB30000:GOTO830
820 GOSUB30000:GOSUB20000
830 GOSUB60800
950 PP=0
970 IFJ1=14ORJ1=15THENPP=PP+J1-4
980 IFY1=14ORY1=15THENPP=PP+Y1-4
990 IFJ1>10ANDJ1<14THENPP=PP+J1-9
1000 IFY1>10ANDY1<14THENPP=PP+Y1-9
1020 IFT=2ANDV=1ORT=1ANDV=0THEN1025
1023 GOTO1030
1025 LOCATE0,12:PRINT"PRENDI TU";PP"PUNTI";:PG=PG+PP:T=2:GOTO1040
1030 LOCATE0,12:PRINT"PRENDO IO";PP"PUNTI";:PC=PC+PP:T=1
1040 IFPP<10THENPRINT" "
1041 GOSUB55000:PAUSE100
1042 CX=19:CY=5:CV=0:CS=0:GOSUB15000
1044 CX=19:CY=10:CV=0:CS=0:GOSUB15000
1048 IFI=33THENCX=26:CY=5:CV=0:CS=0:GOSUB15000:CX=26:CY=10:CV=0:CS=0:GOSUB15000
1050 IFI>34THENC(C1,1)=0:G(G1,1)=0:GOTO1250
1150 IFT=2THENX=G1*6-6:Y=13:CC=0:GOTO1170
1160 X=C1*6-6:Y=2:CC=1
1170 GOSUB45000:GOSUB47000
1180 IFT=1THENX=G1*6-6:Y=13:CC=0:GOTO1210
1200 X=C1*6-6:Y=2:CC=1
1210 GOSUB45000:GOSUB47000
1250 NEXTI
1260 LOCATE0,12:PRINT"   FINE PARTITA   ":PAUSE100
1270 LOCATE0,12
1280 IFPC=PGTHENPRINT"PARITA' 60 - 60   ":GOTO1400
1290 IFPC>PGTHENPRINT"HO VINTO IO!";PC;"-";PG;"  ":GOTO1400
1300 PRINT"HAI VINTO TU";PG;"-";PC;"  "
1400 PAUSE300
1410 LOCATE0,12:PRINT"UN'ALTRA SFIDA (S/N)?"
1420 A=INKEY(0)
1430 A=INKEY(10):IFA=ASC("S")ORA=ASC("s")THEN510
1440 IFA=ASC("N")ORA=ASC("n")THENCLS:PRINT"CIAO":END
1450 GOTO 1430
1970 REM
1980 REM *** INIZIALIZZA CARATTERI SEMI-GRAFICI ***
1990 REM
2000 RESTORE 2100
2010 READA,B:IFA=0THENCLS:RETURN
2020 VP=8192+A*8:FORW=VPTOVP+7:VPOKEW,B:NEXT
2030 GOTO 2010
2090 REM COPPIE DI NUMERI A,B: A=CODICE ASCII, B=COLORI
2100 DATA 130,143,131,143,132,31,133,31,143,47
2110 DATA 217,47,218,47,219,47,220,47,185,79,143,47,144,47
2120 DATA 0,0
2970 REM
2980 REM *** ATTESA DI UN TASTO ***
2990 REM
3000 LOCATE0,23:PRINT" PREMI UN TASTO PER CONTINUARE";:A=INKEY(1)
3010 A=INKEY(10):IFA=0THEN3010
3020 RETURN
9999 REM
10000 REM SUONO DI CARTA STRUSCIATA
10010 SOUND4,10:PAUSE1:SOUND4,0:RETURN
15000 REM DISEGNA UNA CARTA A CX,CY CON VALORE CV E SEME CS
15010 IF CV=0 OR CV=15 THEN 15160
15020 LOCATE CX,CY
15030 PRINT CHR$(213);:FORW=1TO4:PRINTCHR$(157);:NEXT:PRINTCHR$(214);
15040 LOCATE CX,CY+1:IFCV=1THENCV$="A":GOTO15100
15050 IFCV=13THENCV$="R":GOTO15100
15060 IFCV=12THENCV$="D":GOTO15100
15070 IFCV=11THENCV$="F":GOTO15100
15090 CC$=STR$(CV):CV$=MID$(CC$,2,1)
15100 PRINTCHR$(158);CV$;"  ";CHR$(133-CS);CHR$(158);
15110 FORW=2TO6:LOCATE CX,CY+W:PRINTCHR$(158);"    ";CHR$(158);:NEXT
15120 LOCATECX,CY+7:PRINTCHR$(158);CHR$(133-CS);"  ";CV$;CHR$(158);
15130 LOCATECX,CY+8
15140 PRINTCHR$(216);:FORW=1TO4:PRINTCHR$(157);:NEXT:PRINTCHR$(215)
15150 RETURN
15160 IF CV=0THENFORW=0TO8:LOCATECX,CY+W:PRINT"      ";:NEXT:RETURN
15170 LOCATE CX,CY
15180 PRINT CHR$(213);:FORW=1TO4:PRINTCHR$(157);:NEXT:PRINTCHR$(214);
15190 FORW=1TO7:LOCATECX,CY+W:PRINTCHR$(158);:FORJ=1TO4:PRINTCHR$(185);
15200 NEXT:PRINTCHR$(158);:NEXT:LOCATECX,CY+8
15210 PRINTCHR$(216);:FORW=1TO4:PRINTCHR$(157);:NEXT:PRINTCHR$(215)
15220 RETURN
15970 REM
15980 REM *** MANO AL GIOCATORE ***
15990 REM
20000 LOCATE0,12:PRINT"TOCCA A TE, CARTA?":A=INKEY(0)
20010 A=INKEY(10):IFA<ASC("1")ORA>ASC("3")THEN20010
20020 G1=A-48
20030 IFG(G1,1)=0THEN20010
20040 CX=G1*6-6:CY=13:CV=0:CS=0:GOSUB15000
20050 GOSUB10000:CX=19:CY=10:CV=G(G1,1):CS=G(G1,2):GOSUB15000
20060 LOCATE0,12:RETURN
30000 LOCATE0,12:PRINT"TOCCA A ME...     "
30010 REM IFPC>60THEN30100
30020 IFT=2THEN30200
30030 IFT=1THEN32000
30100 PAUSE100
30110 C1=INT(RND(1)*3)+1
30120 IFC(C1,1)=0THEN30110
30130 GOTO39000
30200 REM IL COMPUTER RISPONDE AL GIOCO
30201 BC=0
30202 FORW=1TO3:IFC(W,2)=BRTHENBC=BC+1
30203 NEXT
30204 FORC1=1TO3
30205 IFC(C1,1)=0THENPT(C1)=-100:GOTO31290
30210 GOSUB60800:PP=0
30220 IFJ1=14ORJ1=15THENPP=PP+J1-4
30230 IFY1=14ORY1=15THENPP=PP+Y1-4
30240 IFJ1>10ANDJ1<14THENPP=PP+J1-9
30260 IFY1>10ANDY1<14THENPP=PP+Y1-9
30270 IFV=1THENPT(C1)=-PP:GOTO30300
30280 PT(C1)=PP-.01
30300 IFY1=15ANDY2=BRTHENPT(C1)=PT(C1)-20
30310 IFY2=BRANDBC<2THENPT(C1)=-5
30320 IFY2=BRTHENPT(C1)=PT(C1)-.7-(.1*(Y1-8))
31290 NEXTC1
31300 FP=0:MX=1
31310 FORW=2TO3
31320 IFPT(W)>PT(MX)THENMX=W:GOTO31330
31330 NEXT
31540 C1=MX
31550 GOTO39000
32000 REM GIOCA COMPUTER PER PRIMO
32010 BC=0:FORC1=1TO3
32020 IFC(C1,2)=BRTHENBC=BC+1
32030 NEXTC1
32040 BA=0:FORW=1TO10
32050 IFMT(W,BR+1)=0THENBA=1
32060 NEXT
32070 FORC1=1TO3
32080 GOSUB62000
32090 NEXTC1
32100 CX=1
32110 FORW=2TO3
32120 IFPT(W)>PT(CX)THENCX=W
32130 NEXT
32140 C1=CX
39000 LOCATE0,12
39010 CX=C1*6-6:CY=2:CV=0:CS=0:GOSUB15000
39020 GOSUB10000:CX=19:CY=5:CV=C(C1,1):CS=C(C1,2):GOSUB15000:RETURN
45000 GOSUB10000
45005 IFCC=1THENCX=X:CY=Y:CV=15:CS=1:GOSUB15000:C(C1,1)=MZ(PS,1):C(C1,2)=MZ(PS,2)
45010 IFCC=0THEN45016
45013 GOTO 45020
45016 CX=X:CY=Y:CV=MZ(PS,1):CS=MZ(PS,2):GOSUB15000:G(G1,1)=MZ(PS,1):G(G1,2)=MZ(PS,2)
45020 PS=PS+1
45030 IFCC=1THENGOSUB55010:RETURN
46970 REM
46980 REM SCRIVE LE CARTE RIMASTE
46990 REM
47000 LOCATE26,19:PRINT"CARTE";
47010 LOCATE26,20:PRINT"MAZZO";
47020 LOCATE27,22:PRINT(40-PS+1);
47030 RETURN
49970 REM
49980 REM *** MESCOLAMENTO DEL MAZZO **
49990 REM
50000 FORC=1TO40:MZ(C,1)=0:NEXTC
50008 FORC=0TO3
50010 FORB=1TO10
50015 GOSUB10000
50020 PS=INT(RND(1)*40+1)
50030 IFMZ(PS,1)=0THENMZ(PS,1)=B:MZ(PS,2)=C:GOTO50070
50040 IFPS=40THENPS=0
50050 PS=PS+1
50060 GOTO50030
50070 NEXTB,C
50080 FORC=1TO40
50090 IFMZ(C,1)>7THENMZ(C,1)=MZ(C,1)+3
50100 NEXT
50110 PS=1:RETURN
55000 FL=0:ONG(G1,2)+1GOSUB55100,55200,55300,55400
55010 FL=1:ONC(C1,2)+1GOSUB55100,55200,55300,55400
55020 RETURN
55100 NP=NP+1
55110 IFFL=1THENMT((C(C1,1)+3*(C(C1,1)>10)),1)=1
55120 IFFL=0THENMT((G(G1,1)+3*(G(G1,1)>10)),1)=1
55130 RETURN
55200 NF=NF+1
55210 IFFL=1THENMT((C(C1,1)+3*(C(C1,1)>10)),2)=1
55220 IFFL=0THENMT((G(G1,1)+3*(G(G1,1)>10)),2)=1
55230 RETURN
55300 NQ=NQ+1
55310 IFFL=1THENMT((C(C1,1)+3*(C(C1,1)>10)),3)=1
55320 IFFL=0THENMT((G(G1,1)+3*(G(G1,1)>10)),3)=1
55330 RETURN
55400 NC=NC+1
55410 IFFL=1THENMT((C(C1,1)+3*(C(C1,1)>10)),4)=1
55420 IFFL=0THENMT((G(G1,1)+3*(G(G1,1)>10)),4)=1
55430 RETURN
60800 IFT=2THENJ1=G(G1,1):J2=G(G1,2):Y1=C(C1,1):Y2=C(C1,2):GOTO60850
60840 J1=C(C1,1):J2=C(C1,2):Y1=G(G1,1):Y2=G(G1,2)
60850 IFJ1=1THENJ1=15
60860 IFY1=1THENY1=15
60870 IFJ1=3THENJ1=14
60880 IFY1=3THENY1=14
60890 IFJ2<>Y2ANDY2<>BRTHENV=1:GOTO60950
60910 IFJ2<>Y2ANDY2=BRTHENV=0:GOTO60950
60920 IFJ1>Y1THENV=1:GOTO60950
60930 V=0
60950 RETURN
62000 REM SCELTA CARTA
62010 NN=C(C1,1)
62012 IFNN=0THENPT(C1)=-100:GOTO63000
62020 IFNN=1THENNN=15
62025 IFNN=3THENNN=14
62110 PT(C1)=0
62120 IFNN=14ORNN=15THENPT(C1)=-20-NN
62130 IFBC<2ANDC(C1,2)=BRTHENPT(C1)=-17-NN
62140 IFMT(1,C(C1,2)+1)=0THENPT(C1)=PT(C1)-5
62150 IFMT(3,C(C1,2)+1)=0THENPT(C1)=PT(C1)-4
63000 RETURN
